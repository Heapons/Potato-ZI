// nav utils for generating navmesh on complex multi-stage maps
// also collecting "safe" nav areas ( areas that aren't too small, in death pits, or around the edges of the map )

PZI_CREATE_SCOPE( "__pzi_nav", "PZI_Nav", null, "PZI_NavThink" )

PZI_Nav.AllNavAreas    <- {}
PZI_Nav.SafeNavAreas   <- {}
PZI_Nav.NAV_DEBUG      <- false // draw nav areas
PZI_Nav.MAX_AREAS_PER_TICK <- 150
// function PZI_Nav::FindWalkablePoints() {

// }

GetAllAreas( PZI_Nav.AllNavAreas )

// we don't support maps with no nav for now
if ( !PZI_Nav.AllNavAreas.len() && IsDedicatedServer() ) {

	local msg = "NO NAVMESH FOUND FOR" + MAPNAME + "! SWITCHING TO NEXT MAP\n"

	local fmt = format( "\n%s%s%s\n", msg, msg, msg )

	ClientPrint( null, 3, "\x07FF0000" + msg )
	ClientPrint( null, 3, "\x07FF0000" + msg )
	ClientPrint( null, 3, "\x07FF0000" + msg )

	EntFire( "__pzi_eventwrapper", "Kill" )
	EntFire( "player", "TerminateScriptScope" )
	EntFire( "__pzi_util", "CallScriptFunction", "ChangeLevel", 3 )

	return Assert( false, msg.slice( 0, -1 ) )
}

// pre-collect safe nav areas on load
function PZI_Nav::GetSafeNavAreas() {

	if ( !AllNavAreas.len() )
		return

	PZI_Util.ScriptEntFireSafe( self, "print( `\\n\\n Collecting nav areas, performance warnings will go away shortly...\\n\\n` )", 0.1 )

	local i = 0
	local color_valid  = [   0, 180,  20, 50  ]
	local color_small  = [   0,  20, 180, 50  ]
	local color_edge   = [   0, 180, 180, 50  ]
	local color_warn   = [ 180, 180,  20,  50 ]
	local color_danger = [ 180,   0,  20,  50 ]

	local color = color_valid
	local trace = {}

	// filter out areas that are too small or inside a trigger_hurt
	foreach( name, area in AllNavAreas ) {

		color = color_valid

		if ( area.GetSizeX() < 50 )
			color = color_small

		else if ( area.GetSizeY() < 50 )
			color = color_small

		else if ( PZI_Util.IsPointInTrigger( area.GetCenter(), "trigger_hurt" ) )
			color = color_danger

		else {

			// this is too broken atm
			// for ( local i = 0; i < NUM_DIRECTIONS; i++ )
				// if ( !area.GetAdjacentArea( i, 1 ) ) {

					// color = color_edge
					// break
				// }

			if ( color != color_edge ) {

				// box trace straight up and see if there's a trigger_hurt above us
				trace.clear()
				trace.start <- area.GetCenter()
				trace.end 	<- area.GetCenter() + Vector( 0, 0, INT_MAX )
				trace.mask  <- CONTENTS_SOLID
				trace.hullmin <- area.GetCorner( NORTH_WEST )
				trace.hullmax <- area.GetCorner( SOUTH_EAST )
	
				TraceHull( trace )
	
				if ( trace.hit && trace.enthit && trace.enthit.GetClassname() == "trigger_hurt" )
					color = color_warn

			}
		}

		if ( color == color_valid )
			SafeNavAreas[name] <- area

		if ( NAV_DEBUG )
			area.DebugDrawFilled( color[0], color[1], color[2], color[3], 3.0, true, 0.0 )

		i++

		if ( !( i % MAX_AREAS_PER_TICK ) ) // process this many nav areas per tick
			yield SafeNavAreas.len()
	}

	print( "\n\nSafe nav areas collected\n\n" )
}

local gen = PZI_Nav.GetSafeNavAreas()
// resume gen // do one step right away before thinking

function PZI_Nav::ThinkTable::PopulateSafeNav() {

	if ( gen.getstatus() == "dead" ) {

		delete ThinkTable.PopulateSafeNav
		return 1
	}
	local result = resume gen || SafeNavAreas.len()
	printf( "Safe Areas: %s / %s\n", result.tostring(), AllNavAreas.len().tostring() )
}

function PZI_Nav::GetRandomSafeArea() { return SafeNavAreas.values()[RandomInt( 0, SafeNavAreas.len() - 1 )] }

// NAV GENERATION TOOLS BELOW
function PZI_Nav::NavGenerator() {

	local player = GetListenServerHost()

	local walkable_points = []
	local e
	foreach( i, ent in [ "info_player_teamspawn", "item_teamflag", "team_control_point", "trigger_capture_area", "func_capturezone" ] ) {

		yield printl( "collecting: " + ent ), true

		while ( e = FindByClassname( e, ent ) )
			walkable_points.append( e.GetCenter() )
	}

	foreach ( track, _ in PZI_MapLogic.payload_tracks )
		if ( track )
			walkable_points.append( track.GetCenter() )

	local points_len = walkable_points.len()

	printl( "WALKABLE POINTS: " + points_len )

	local generate_delay = 0.0
	// Process spawn points for current arena
	foreach( i, point in walkable_points ) {

		if ( !point )
			continue

		generate_delay += 0.01
		PZI_Util.ScriptEntFireSafe( player, format( @"

			local origin = Vector( %f, %f, %f )
			self.SetAbsOrigin( origin )
			self.SnapEyeAngles( QAngle( 90, 0, 0 ) )
			SendToConsole( `nav_mark_walkable` )
			local progress = ( %d + 1 )
			local total = %d
			local str = `Marking Spawn Point: ` + origin.ToKVString() + ` Progress: ` + progress + ` / ` + total
			printl( str )
			ClientPrint( null, 3, str )

		", point.x, point.y, point.z, i, points_len ), generate_delay, null, null )

		yield true
	}

	// Schedule nav generation
	PZI_Util.ScriptEntFireSafe( PZI_Util.Worldspawn, @"

		ClientPrint( null, 3, `Areas marked!` )
		ClientPrint( null, 3, `Generating nav...` )
		SendToConsole( `nav_generate` )

	", generate_delay + 0.5 )

	yield true


	AddThinkToEnt( player, null )
}

function PZI_Nav::CreateNav() {

	if ( FileToString( "__NAV_DISCONNECT" ) == "1" )
		return DisconnectAreas(), StringToFile( "__NAV_DISCONNECT", "0" )

	EntFire( "team_round_timer", "Pause" )
	SendToConsole( "host_thread_mode 0; developer 1" )

	local player = GetListenServerHost()

	player.SetMoveType( MOVETYPE_NOCLIP, MOVECOLLIDE_DEFAULT )

	scope <- PZI_Util.GetEntScope( player )

	local gen = NavGenerator()

	function scope::NavThink() {

		if ( gen.getstatus() != "dead" )
			resume gen

		else if ( GetInt( "host_thread_mode" ) ) {

			StringToFile( "__NAV_DISCONNECT", "1" )
			// EntFire( "__pzi_nav", "CallScriptFunction", "DisconnectAreas", 1 )
			// PZI_Util.ScriptEntFireSafe( player, "SendToConsole(`nav_analyze`)", 2 )
		}

		return 0.05
	}

	AddThinkToEnt( player, "NavThink" )
}

// nav area disconnect: Modified from scripts by Mikusch & ficool2

function CTFNavArea::ComputePortal( to, dir )
{
	local center = Vector()
	local nwCorner = GetCorner( NORTH_WEST )
	local seCorner = GetCorner( SOUTH_EAST )
	local to_nwCorner = to.GetCorner( NORTH_WEST )
	local to_seCorner = to.GetCorner( SOUTH_EAST )

	if ( dir == NORTH || dir == SOUTH )
	{
		if ( dir == NORTH )
			center.y = nwCorner.y
		else
			center.y = seCorner.y

		local left = ( nwCorner.x > to_nwCorner.x ) ? nwCorner.x : to_nwCorner.x
		local right = ( seCorner.x < to_seCorner.x ) ? seCorner.x : to_seCorner.x

		if ( left < nwCorner.x )
			left = nwCorner.x
		else if ( left > seCorner.x )
			left = seCorner.x

		if ( right < nwCorner.x )
			right = nwCorner.x
		else if ( right > seCorner.x )
			right = seCorner.x

		center.x = ( left + right ) * 0.5
	}
	else
	{
		if ( dir == WEST )
			center.x = nwCorner.x
		else
			center.x = seCorner.x

		local top = ( nwCorner.y > to_nwCorner.y ) ? nwCorner.y : to_nwCorner.y
		local bottom = ( seCorner.y < to_seCorner.y ) ? seCorner.y : to_seCorner.y

		if ( top < nwCorner.y )
			top = nwCorner.y
		else if ( top > seCorner.y )
			top = seCorner.y

		if ( bottom < nwCorner.y )
			bottom = nwCorner.y
		else if ( bottom > seCorner.y )
			bottom = seCorner.y

		center.y = ( top + bottom ) * 0.5
	}

	center.z = GetZ( center )
	return center
}

function CTFNavArea::GetClosestPointOnArea( pos )
{
	local close = Vector()
	local nwCorner = GetCorner( NORTH_WEST )
	local seCorner = GetCorner( SOUTH_EAST )
	close.x = ( pos.x - nwCorner.x >= 0 ) ? pos.x : nwCorner.x
	close.x = ( close.x - seCorner.x >= 0 ) ? seCorner.x : close.x
	close.y = ( pos.y - nwCorner.y >= 0 ) ? pos.y : nwCorner.y
	close.y = ( close.y - seCorner.y >= 0 ) ? seCorner.y : close.y
	close.z = GetZ( close )
	return close
}

function PZI_Nav::DisconnectAreas()
{
	GetAllAreas( AllNavAreas )

	function DisconnectAreaGenerator() {

		foreach ( i, area in AllNavAreas )
		{
			local center = area.GetCenter()
			for ( local dir = 0; dir < NUM_DIRECTIONS; dir++ )
			{
				local adjacentAreas = {}
				area.GetAdjacentAreas( dir, adjacentAreas )

				foreach ( j, adjacentArea in adjacentAreas )
				{
					local pos = area.ComputePortal( adjacentArea, dir )
					local from = pos + Vector()
					local to = pos + Vector()
					from.z = area.GetZ( from )
					to.z = adjacentArea.GetZ( to )

					to = adjacentArea.GetClosestPointOnArea( to )

					if ( (to.z - from.z ) > STEP_HEIGHT )
					{
						area.DebugDrawFilled( 0, 255, 0, 32, 15, true, 0 )
						adjacentArea.DebugDrawFilled( 255, 0, 0, 32, 15, true, 0 )
						DebugDrawLine( from, to, 255, 255, 255, true, 15 )

						area.Disconnect( adjacentArea )
						printf( "Disconnected area #%d from area #%d\n", area.GetID(), adjacentArea.GetID() )
					}
				}
			}

			if ( !( i % MAX_AREAS_PER_TICK ) )
				yield true
		}
	}

	local gen = DisconnectAreaGenerator()
	function ThinkTable::DisconnectAreaThink() {

		if ( gen.getstatus() == "dead" )
			return SendToConsole("nav_save"), delete ThinkTable.DisconnectAreaThink

		resume gen
		return 0.05
	}
}